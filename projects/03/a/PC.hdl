// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    // Copied from https://github.com/jcoglan/nand2tetris/blob/master/projects/03/a/PC.hdl

    // Increments current value by 1
    Inc16(in=feedback, out=incout);

    // If inc == 1, out == Inc16 out value else == current value (feedback)
    Mux16(a=feedback, b=incout, sel=inc, out=m1);

    // Choose the value between the current value (m1) or a new value inserted (in)
    Mux16(a=m1, b=in, sel=load, out=m2);

    // Reset the value to zero (false) if reset == 1 else out == current value (m2)
    Mux16(a=m2, b=false, sel=reset, out=m3);

    // Register current value
    Register(in=m3, load=true, out=feedback, out=out);
    
}
